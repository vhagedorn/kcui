plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'org.jetbrains.kotlin.jvm' version '1.5.30'
}

group 'me.vadim'
version '1.0.2'

def name = project.name + '.jar'
def main = group + '.ja.Main'
def jver = '11'

project.buildDir project.projectDir.path + '/target'

repositories {
    mavenCentral()
    mavenLocal()

    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://oss.sonatype.org/content/repositories/central' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    // core libs
    implementation 'org.jetbrains:annotations:24.0.1'
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.5.30'

    // Swing
    implementation 'com.github.mslxl:KtSwing:2.1.2'
    implementation 'com.formdev:flatlaf:3.0'
    implementation 'com.formdev:flatlaf-intellij-themes:3.0'
    implementation 'org.imgscalr:imgscalr-lib:4.2'
//    implementation 'com.miglayout:miglayout-swing:4.2'

    // HTML->PDF conversion
    implementation 'org.jsoup:jsoup:1.15.3'
    implementation 'org.apache.pdfbox:pdfbox:2.0.27'
    implementation 'io.github.fanyong920:jvppeteer:1.1.5'

    // render.cache
    implementation 'com.j256.ormlite:ormlite-core:6.1'
    implementation 'com.j256.ormlite:ormlite-jdbc:6.1'
    implementation 'org.xerial:sqlite-jdbc:3.42.0.0'

    // XML storage
    implementation 'org.glassfish.jaxb:jaxb-core:4.0.3'
    implementation 'org.glassfish.jaxb:jaxb-xjc:4.0.3'
    implementation 'org.glassfish.jaxb:jaxb-runtime:4.0.3'

    // Google code
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.google.guava:guava:31.1-jre'

    // Stroke order diagrams
    implementation 'org.apache.xmlgraphics:batik-transcoder:1.16'
    implementation 'org.apache.xmlgraphics:batik-svggen:1.16'
    implementation 'org.apache.xmlgraphics:batik-dom:1.16'
    implementation 'org.apache.xmlgraphics:batik-codec:1.16'
//    implementation 'org.apache.xmlgraphics:xmlgraphics-commons:2.8'
//    implementation 'org.apache.xmlgraphics:batik-bridge:1.16'
//    implementation 'org.apache.xmlgraphics:batik-css:1.16'
//    implementation 'org.apache.xmlgraphics:batik-ext:1.16'
//    implementation 'org.apache.xmlgraphics:batik-gvt:1.16'
//    implementation 'org.apache.xmlgraphics:batik-swing:1.16'

    implementation 'org.slf4j:slf4j-nop:2.0.6' // get rid of bullshit warning messages
}

//generate `version` file to add to jar

ext.genOutputDir = file("$buildDir/generated")

tasks.register('generatePropInfo') {
    ext.outputFile = file("$genOutputDir/version")
    outputs.file(outputFile)
    doLast {
        outputFile.text = project.version
    }
}

sourceSets.main.output.dir genOutputDir, builtBy: generatePropInfo

shadowJar {
    archiveFileName.set("ui.jar")
    destinationDirectory.set(project.buildDir)
    mergeServiceFiles()
//    minimize()
    manifest {
        attributes('Main-Class': main)
    }
}

build {
    dependsOn(shadowJar)
}

compileJava {
    targetCompatibility = jver
    sourceCompatibility = jver
}

compileKotlin {
    targetCompatibility jver
    sourceCompatibility jver

    kotlinOptions {
        jvmTarget = jver
        freeCompilerArgs = ['-Xjvm-default=all', '-Xopt-in=kotlin.RequiresOptIn']
    }
}


jar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    destinationDirectory.set(project.buildDir)
    archiveFileName.set(project.name + '-' + project.version + '-unshaded.jar')
}